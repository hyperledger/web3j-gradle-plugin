plugins {
    id "com.gradle.plugin-publish" version "0.10.0"
    id 'com.jfrog.bintray' version '1.8.4'
    id 'java-gradle-plugin'
    id 'java'

    // Required for JFrog Artifactory repository
    id 'jacoco'
    id 'maven-publish'

    // Required for Maven Nexus repository
    id 'maven'
    id 'signing'
    id 'idea'
}

group 'org.web3j'
version '0.1.4'
description = 'web3j Gradle Plugin'
sourceCompatibility = 1.8

jacoco {
    toolVersion = '0.8.1' // See http://www.eclemma.org/jacoco/.
}

jacocoTestReport {
    reports {
        xml.enabled = true
    }
}

ext {
    web3jVersion = '3.6.0'
    solidityPluginVersion = '0.1.2'
    junitVersion = '4.12'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.web3j:solidity-gradle-plugin:$solidityPluginVersion"
    compile("org.web3j:console:$web3jVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    testCompile "junit:junit:$junitVersion"
}

gradlePlugin {
    plugins {
        web3j {
            id = 'org.web3j'
            implementationClass = 'org.web3j.gradle.plugin.Web3jPlugin'
        }
    }
}

pluginBundle {
    website = 'https://web3j.io/'
    vcsUrl = 'https://github.com/web3j/web3j-gradle-plugin'
    description = 'Gradle plugin providing tasks to generate web3j contracts from Solidity.'
    tags = ['solidity', 'ethereum', 'web3.js', 'web3j']
    plugins {
        web3j {
            // id is captured from java-gradle-plugin configuration
            displayName = project.description
        }
    }
}

tasks.withType(Test) {
    reports.html.destination file("${reporting.baseDir}/${name}")
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

def pomConfig = {
    scm {
        url 'https://github.com/web3j/web3j-gradle-plugin'
        connection 'scm:https://github.com/web3j/web3j-gradle-plugin.git'
        developerConnection 'scm:git://github.com/web3j/web3j-gradle-plugin.git'
    }
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'conor10'
            name 'Conor Svensson'
            email 'conor10@gmail.com'
        }
        developer {
            id 'xaviarias'
            name 'Xavier Arias'
            email 'xavi.arias.segui@gmail.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version project.version
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.description)
                root.appendNode('description', 'Gradle plugin providing tasks to generate web3j contracts from Solidity.')
                root.appendNode('url', 'https://github.com/web3j/web3j-gradle-plugin')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?
            project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?
            project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        userOrg = 'web3j'
        licenses = ['Apache-2.0']
        issueTrackerUrl = 'https://github.com/web3j/web3j-gradle-plugin/issues'
        vcsUrl = 'https://github.com/web3j/web3j-gradle-plugin.git'
        websiteUrl = 'http://web3j.io'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            vcsTag = project.version
            released = new Date()
            gpg {
                sign = true
                passphrase = project.property('signing.password')
            }
            mavenCentralSync {
                sync = true
                user = project.property('nexusUsername')
                password = project.property('nexusPassword')
                close = '1'
            }
        }
    }
}

task release {
    dependsOn 'clean'
    dependsOn 'build'
    dependsOn 'bintrayUpload'

    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('bintrayUpload').mustRunAfter 'build'
}
